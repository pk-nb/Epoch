{div, form, button, p, h1, hr, span, img, input, label, a} = React.DOM

unless Array::filter
  Array::filter = (callback) ->
    element for element in this when callback(element)


TimelinePanel = React.createClass
  displayName: 'TimelinePanel'

  onPostSuccess: (data) ->
      console.log data
      console.log @props.timelines
      timelines = @props.timelines
      timelines.push(data)
      #
      @props.setAppState(timelines: timelines)

  onPostError: (messages) ->
    # Render error by setting state
    console.log 'Ajax failure'
    console.log(messages)
    @props.setAppState(timeline_errors: messages)


  handleSubmit: (e) ->
    console.log 'Submit form'
    e.preventDefault()
    $.ajax {
      type: 'POST',
      url: Routes.timelines_path(),
      data: $(@refs.newTimelineForm.getDOMNode()).serialize(),
      dataType: 'json'

      success: (data, status, xhr) =>
        console.log 'Ajax success'
        @onPostSuccess(data)
        @refs.newTimelineForm.getDOMNode().reset()

      error: (xhr, status, error) =>
        # Retrieve/parse error messages from response text
        messages = [];
        try messages = JSON.parse(xhr.responseText).errors
        catch e then messages = [xhr.responseText]
        @onPostError(messages)
    }

  unselectedTimelines: ->
    tIds = @props.timelines.map (timeline) ->
      timeline.id
    @props.userTimelines.filter (userTimeline) =>
       userTimeline.id not in tIds


  addSelectedTimeline: (data) ->
    newTimelines = $.extend(true, [], @props.timelines)
    newTimelines.push(data)
    @props.setAppState(timelines: newTimelines)


  handleSelectTimeline: (id) ->
    =>
      # TODO Start spinner state here and end spinner state in success method

      # Request timeline and append to selected timelines
      $.ajax {
        type: 'GET',
        url: Routes.timeline_path(id),
        dataType: 'json'

        success: (data, status, xhr) =>
          @addSelectedTimeline(data)

        error: (xhr, status, error) =>
          # TODO Error handling in view with state
          console.log error
      }

  handleRemoveTimeline: (id) ->
    =>
      newTimelines = $.extend(true, [], @props.timelines).filter (timeline) ->
        timeline.id != id
      @props.setAppState(timelines: newTimelines)

  userTimelinesSection: ->
    if @props.user
      div null,
        h1 null,
          "#{@props.user.name}'s Timelines"
        @userTimelineList()

  userTimelineList: ->
    @unselectedTimelines().map (timeline) =>
      div {key: timeline.title + timeline.id, className: 'timeline-item'},
        div className: 'title',
          div className: 'circle',
            ''
          span null,
            timeline.title
        div className: 'icons',
          img { onClick: @handleSelectTimeline(timeline.id), className: 'plus-icon', src: '<%= asset_path 'plus-icon.svg' %>'}

  timelineList: ->
    if @props.timelines.length > 0
      @props.timelines.map (timeline) =>
        div {key: timeline.title, className: 'timeline-item'},
          div className: 'title',
            div className: 'circle',
              ''
            span null,
              timeline.title
          div className: 'icons',
            img { onClick: @handleRemoveTimeline(timeline.id), className: 'x-icon', src: '<%= asset_path 'x-icon.svg' %>'}
    else
      p null,
        'No Selected Timelines'

  tryRenderTwitter: (providers, UI) ->
    if 'twitter' in providers
      UI.NewTweetPanel
        key: 'newTweetPanel',
        user: @props.user
        timelines: @props.timelines
        userTimelines: @props.userTimelines
        tweet_errors: @props.tweet_errors
        setAppState: @props.setAppState
    else
      div null,
        h1 null,
          'Sign in to Twitter to import tweets'
        a { href: '/auth/twitter', id: 'sign_in_twitter', className: 'button' },
            'Sign in with Twitter'

  tryRenderGithub: (providers, UI) ->
    if 'github' in providers
      UI.NewRepoPanel
        key: 'newRepoPanel',
        user: @props.user
        timelines: @props.timelines
        userTimelines: @props.userTimelines
        repo_errors: @props.repo_errors
        repos: @props.repos
        setAppState: @props.setAppState
    else
      div null,
        h1 null,
            'Sign in to Github to import repositories'
        a { href: '/auth/github', id: 'sign_in_github', className: 'button' },
            'Sign in with Github'

  render: ->
    UI = window.EpochUI
    providers = if @props.user == null then [] else @props.user.providers
    div className: 'panel timeline-panel',
      h1 null,
        'Selected Timelines'
      @timelineList(),
      @userTimelinesSection(),
      UI.NewTimelinePanel
        key: 'newTimelinePanel',
        user: @props.user
        timelines: @props.timelines
        userTimelines: @props.userTimelines
        timeline_errors: @props.timeline_errors
        setAppState: @props.setAppState
      @tryRenderGithub(providers, UI)
      @tryRenderTwitter(providers, UI)


@.EpochUI ?= {}
@.EpochUI.TimelinePanel = TimelinePanel
