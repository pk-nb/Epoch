{div, p, a, button, img, span} = React.DOM

UserNameMixin =
  user: ->
    if @props.user
      div className: 'shelf-user',
        p { onClick: @props.handleClick(@props.panelIds.user), className: 'dropdown-link' },
          img {className: 'avatar', src: @props.user.picture},
          span null,
            @props.user.name
    else
      p { onClick: @props.handleClick(@props.panelIds.user), className: 'dropdown-link' },
        'Login'

TimelineTitleMixin =
  # TODO Truncate more than three titles if large set of timelines
  timelineTitle: ->
    keys = Object.keys(@props.timelines)
    if keys.length > 0
      keys.reduce (prev, key) =>
        prev + @props.timelines[key].title + ', '
      , ''
    else
      'Select Timelines'


DefaultTopShelf = React.createClass
  displayName: 'DefaultTopShelf'
  mixins: [UserNameMixin, TimelineTitleMixin]

  render: ->
    div className: 'shelf',
      div className: 'left',
        p className: 'logo',
          'Epoch'
      div className: 'center',
        p { onClick: @props.handleClick(@props.panelIds.timeline), className: 'dropdown-link'},
          @timelineTitle()
      div className: 'right',
        @user()


UserShelf = React.createClass
  displayName: 'UserShelf'
  mixins: [UserNameMixin]

  render: ->
    div className: 'shelf',
      div className: 'left',
        ''
      div className: 'center',
        p null,
          if @props.user then 'User Profile' else 'Login'
      div className: 'right',
        img {onClick: @props.handleClick(), className: 'shelf-icon', src: '<%= asset_path 'x-icon.svg' %>'}


TimelineShelf = React.createClass
  displayName: 'TimelineShelf'

  render: ->
    div className: 'shelf',
      div className: 'left',
        img {onClick: @props.handleClick(), className: 'shelf-icon', src: '<%= asset_path 'x-icon.svg' %>'}
      div className: 'center',
        p { onClick: @props.handleClick(), className: 'dropdown-link'},
          'Select Timelines'
      div className: 'right',
        ''


@.EpochUI ?= {}
@.EpochUI.DefaultTopShelf = DefaultTopShelf
@.EpochUI.UserShelf = UserShelf
@.EpochUI.TimelineShelf = TimelineShelf
